/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_diesel_NS200_purchase_green, "gfx/purchase/diesel/NS200_green.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS200_purchase_yellow, "gfx/purchase/diesel/NS200_yellow.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS200_purchase_red, "gfx/purchase/diesel/NS200_red.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_diesel_NS200_purchase, current_year) {
	0..1970: spriteset_diesel_NS200_purchase_green;
    1971..1998: spriteset_diesel_NS200_purchase_yellow;
    spriteset_diesel_NS200_purchase_red;
}

/*
* VEHICLE
*/

spriteset(spriteset_diesel_NS200_green, "gfx/diesel/NS200.png") {
	template_2cc_48(1, 1)
}
spriteset(spriteset_diesel_NS200_yellow, "gfx/diesel/NS200.png") {
	template_2cc_48(1, 32)
}
spriteset(spriteset_diesel_NS200_red, "gfx/diesel/NS200.png") {
	template_2cc_48(1, 64)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_diesel_NS200_randomlivery_yellow_red) {
	14: spriteset_diesel_NS200_yellow;
    2: spriteset_diesel_NS200_red;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS200_autolivery, date_of_last_service) {
	0..date(1970,12,31): spriteset_diesel_NS200_green;
    date(1971,1,1)..date(1998,12,31): spriteset_diesel_NS200_yellow;
    switch_diesel_NS200_randomlivery_yellow_red;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS200_purchaselivery, build_year) {
	0..1970: spriteset_diesel_NS200_green;
    1971..1998: spriteset_diesel_NS200_yellow;
    switch_diesel_NS200_randomlivery_yellow_red;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS200_livery, cargo_subtype) {
	1: switch_diesel_NS200_purchaselivery;
    2: spriteset_diesel_NS200_green;
    3: spriteset_diesel_NS200_yellow;
    4: spriteset_diesel_NS200_red;
    switch_diesel_NS200_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_diesel_NS200_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_green);
    3: return string(str_cargo_subtype_yellow);
    4: return string(str_cargo_subtype_red);
    return CB_RESULT_NO_TEXT;
}
