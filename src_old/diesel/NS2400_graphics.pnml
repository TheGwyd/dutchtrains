/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_diesel_NS2400_purchase_blue, "gfx/purchase/diesel/NS2400_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS2400_purchase_bordeaux, "gfx/purchase/diesel/NS2400_bordeaux.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS2400_purchase_grey, "gfx/purchase/diesel/NS2400_grey.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_diesel_NS2400_purchase, current_year) {
	0..1955: spriteset_diesel_NS2400_purchase_blue;
    1956..1970: spriteset_diesel_NS2400_purchase_bordeaux;
    spriteset_diesel_NS2400_purchase_grey;
}

/*
* VEHICLE
*/

spriteset(spriteset_diesel_NS2400_blue, "gfx/diesel/NS2400.png") {
	template_2cc_78(1, 1)
}
spriteset(spriteset_diesel_NS2400_brown, "gfx/diesel/NS2400.png") {
	template_2cc_78(1, 32)
}
spriteset(spriteset_diesel_NS2400_grey, "gfx/diesel/NS2400.png") {
	template_2cc_78(1, 64)
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, SELF, switch_diesel_NS2400_autolivery, date_of_last_service) {
	0..date(1955,12,31): spriteset_diesel_NS2400_blue;
    date(1956,1,1)..date(1970,12,31): spriteset_diesel_NS2400_brown;
    spriteset_diesel_NS2400_grey;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS2400_purchaselivery, build_year) {
	0..1955: spriteset_diesel_NS2400_blue;
    1956..1970: spriteset_diesel_NS2400_brown;
    spriteset_diesel_NS2400_grey;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS2400_livery, cargo_subtype) {
	1: switch_diesel_NS2400_purchaselivery;
    2: spriteset_diesel_NS2400_blue;
    3: spriteset_diesel_NS2400_brown;
    4: spriteset_diesel_NS2400_grey;
    switch_diesel_NS2400_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_diesel_NS2400_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_blue);
    3: return string(str_cargo_subtype_brown);
    4: return string(str_cargo_subtype_grey);
    return CB_RESULT_NO_TEXT;
}
