/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_steam_NCS21_purchase, "gfx/purchase/steam/NCS21.png") {
	template_purchase(0, 0)
}

spriteset(spriteset_steam_SS481_purchase, "gfx/purchase/steam/SS481.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_livery_purchase, current_year) {
	0..1919: spriteset_steam_NCS21_purchase;
    spriteset_steam_SS481_purchase;
}

/*
* VEHICLE
*/

//6x engine animation
spriteset(spriteset_steam_NCS21_ani_1, "gfx/steam/NCS21.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_steam_NCS21_ani_2, "gfx/steam/NCS21.png") {
	template_2cc_68(1, 32)
}
spriteset(spriteset_steam_NCS21_ani_3, "gfx/steam/NCS21.png") {
	template_2cc_68(1, 64)
}
spriteset(spriteset_steam_NCS21_ani_4, "gfx/steam/NCS21.png") {
	template_2cc_68(1, 96)
}
//tender
spriteset(spriteset_steam_NCS21_tender, "gfx/steam/NCS21.png") {
	template_2cc_38(1, 128)
}

switch(FEAT_TRAINS, PARENT, switch_steam_NCS21_animation, (motion_counter)) {
    2..3:   spriteset_steam_NCS21_ani_2;
    4..5:   spriteset_steam_NCS21_ani_3;
    6..7:   spriteset_steam_NCS21_ani_4;
    
    10..11: spriteset_steam_NCS21_ani_2;
    12..13: spriteset_steam_NCS21_ani_3;
    14..15: spriteset_steam_NCS21_ani_4;
    spriteset_steam_NCS21_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_NCS21_tender;
    switch_steam_NCS21_animation;
}

//6x engine animation
spriteset(spriteset_steam_SS481_ani_1, "gfx/steam/SS481.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_steam_SS481_ani_2, "gfx/steam/SS481.png") {
	template_2cc_68(1, 32)
}
spriteset(spriteset_steam_SS481_ani_3, "gfx/steam/SS481.png") {
	template_2cc_68(1, 64)
}
spriteset(spriteset_steam_SS481_ani_4, "gfx/steam/SS481.png") {
	template_2cc_68(1, 96)
}
//tender
spriteset(spriteset_steam_SS481_tender, "gfx/steam/SS481.png") {
	template_2cc_38(1, 128)
}

switch(FEAT_TRAINS, PARENT, switch_steam_SS481_animation, (motion_counter)) {
    2..3:   spriteset_steam_SS481_ani_2;
    4..5:   spriteset_steam_SS481_ani_3;
    6..7:   spriteset_steam_SS481_ani_4;
    
    10..11: spriteset_steam_SS481_ani_2;
    12..13: spriteset_steam_SS481_ani_3;
    14..15: spriteset_steam_SS481_ani_4;
    spriteset_steam_SS481_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_SS481_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_SS481_tender;
    switch_steam_SS481_animation;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_steam_NCS21_autolivery, date_of_last_service) {
	0..date(1919,12,31): switch_steam_NCS21_position;
    switch_steam_SS481_position;
}

switch(FEAT_TRAINS, PARENT, switch_steam_NCS21_purchaselivery, build_year) {
	0..1919: switch_steam_NCS21_position;
    switch_steam_SS481_position;
}

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_livery, cargo_subtype) {
	1: switch_steam_NCS21_purchaselivery;
    2: switch_steam_NCS21_position;
    3: switch_steam_SS481_position;
    switch_steam_NCS21_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_steam_NCS21_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_NBDS);
    3: return string(str_cargo_subtype_SS_NS);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_articulated, extra_callback_info1) {
    1: return item_steam_NCS21;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VEHICLE LENGTH CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_length, (position_in_vehid_chain % 2)) {
    1: return 3;
    return 6;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_NCS21_visual_effect_and_powered, (position_in_vehid_chain % 2)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
}
