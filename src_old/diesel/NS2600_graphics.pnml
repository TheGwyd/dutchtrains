/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_diesel_NS2600_purchase_brown, "gfx/purchase/diesel/NS2600_brown.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS2600_purchase_blue, "gfx/purchase/diesel/NS2600_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_diesel_NS2600_purchase, current_year) {
	0..1953: spriteset_diesel_NS2600_purchase_brown;
    spriteset_diesel_NS2600_purchase_blue;
}

/*
* VEHICLE
*/

spriteset(spriteset_diesel_NS2600_brown, "gfx/diesel/NS2600.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_diesel_NS2600_blue, "gfx/diesel/NS2600.png") {
	template_2cc_88(1, 32)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_diesel_NS2600_randomlivery) {
	4: spriteset_diesel_NS2600_brown;
    2: spriteset_diesel_NS2600_blue;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS2600_autolivery, date_of_last_service) {
	0..date(1953,12,31): spriteset_diesel_NS2600_brown;
    switch_diesel_NS2600_randomlivery;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS2600_purchaselivery, build_year) {
	0..1953: spriteset_diesel_NS2600_brown;
    spriteset_diesel_NS2600_blue;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS2600_livery, cargo_subtype) {
	1: switch_diesel_NS2600_purchaselivery;
    2: spriteset_diesel_NS2600_brown;
    3: spriteset_diesel_NS2600_blue;
    switch_diesel_NS2600_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_diesel_NS2600_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_brown);
    3: return string(str_cargo_subtype_blue);
    return CB_RESULT_NO_TEXT;
}
