/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_Lint41H_purchase_syntus, "gfx/purchase/dmu/Lint41H_syntus.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_Lint41H_purchase_twents, "gfx/purchase/dmu/Lint41H_twents.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_dmu_Lint41H_purchase, current_year) {
	0..2006: spriteset_dmu_Lint41H_purchase_syntus;
    spriteset_dmu_Lint41H_purchase_twents;
}

/*
* VEHICLE
*/

//syntus
spriteset(spriteset_dmu_Lint41H_Syntus_front, "gfx/dmu/Lint41H.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_dmu_Lint41H_Syntus_back, "gfx/dmu/Lint41H.png") {
	template_2cc_88(1, 32)
}
//Twents
spriteset(spriteset_dmu_Lint41H_Twents_front, "gfx/dmu/Lint41H.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_dmu_Lint41H_Twents_back, "gfx/dmu/Lint41H.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_position_syntus, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_Lint41H_Syntus_back;
    spriteset_dmu_Lint41H_Syntus_front;
}

switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_position_Twents, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_Lint41H_Twents_back;
    spriteset_dmu_Lint41H_Twents_front;
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_dmu_Lint41H_randomlivery_syntus_twents) {
	1: switch_dmu_Lint41H_position_syntus;
    1: switch_dmu_Lint41H_position_Twents;
}

switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_randomlivery, build_year) {
	0..2006: switch_dmu_Lint41H_position_syntus;
    switch_dmu_Lint41H_randomlivery_syntus_twents;
}

switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_livery, cargo_subtype) {
    1: switch_dmu_Lint41H_position_syntus;
    2: switch_dmu_Lint41H_position_Twents;
    switch_dmu_Lint41H_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
    1: return string(str_cargo_subtype_Syntus);
    2: return string(str_cargo_subtype_Twents);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_Lint41H_articulated, extra_callback_info1) {
    1: return item_dmu_Lint41H;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
