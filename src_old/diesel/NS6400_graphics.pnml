/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_diesel_NS6400_purchase_yellow_grey, "gfx/purchase/diesel/NS6400_yellow_grey.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS6400_purchase_grey_blue, "gfx/purchase/diesel/NS6400_grey_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_NS6400_purchase_yellow_grey_red, "gfx/purchase/diesel/NS6400_yellow_grey_red.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_diesel_NS6400_purchase, current_year) {
	0..1998: spriteset_diesel_NS6400_purchase_yellow_grey;
    2006..2009: spriteset_diesel_NS6400_purchase_grey_blue;
    spriteset_diesel_NS6400_purchase_yellow_grey_red;
}

/*
* VEHICLE
*/

spriteset(spriteset_diesel_NS6400_ns, "gfx/diesel/NS6400.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_diesel_NS6400_railion, "gfx/diesel/NS6400.png") {
	template_2cc_68(1, 32)
}
spriteset(spriteset_diesel_NS6400_railion_red, "gfx/diesel/NS6400.png") {
	template_2cc_68(1, 64)
}
spriteset(spriteset_diesel_NS6400_railpro, "gfx/diesel/NS6400.png") {
	template_2cc_68(1, 96)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_randomlivery_railion) {
	16: spriteset_diesel_NS6400_railion;
    8: spriteset_diesel_NS6400_railion_red;
    96: spriteset_diesel_NS6400_ns;
}

random_switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_randomlivery_railpro) {
	16: spriteset_diesel_NS6400_railion;
    8: spriteset_diesel_NS6400_railion_red;
    4: spriteset_diesel_NS6400_railpro;
    92: spriteset_diesel_NS6400_ns;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_autolivery, date_of_last_service) {
	0..date(1998,12,31): spriteset_diesel_NS6400_ns;
    date(2006,1,1)..date(2009,12,31): switch_diesel_NS6400_randomlivery_railpro;
    switch_diesel_NS6400_randomlivery_railion;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_purchaselivery, build_year) {
	0..1998: spriteset_diesel_NS6400_ns;
    2006..2009: switch_diesel_NS6400_randomlivery_railpro;
    switch_diesel_NS6400_randomlivery_railion;
}

switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_livery, cargo_subtype) {
	1: switch_diesel_NS6400_purchaselivery;
    2: spriteset_diesel_NS6400_ns;
    3: spriteset_diesel_NS6400_railion;
    4: spriteset_diesel_NS6400_railion_red;
    5: spriteset_diesel_NS6400_railpro;
    switch_diesel_NS6400_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_diesel_NS6400_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_NS);
    3: return string(str_cargo_subtype_Railion);
    4: return string(str_cargo_subtype_Railion_red);
    5: return string(str_cargo_subtype_Railpro);
    return CB_RESULT_NO_TEXT;
}

