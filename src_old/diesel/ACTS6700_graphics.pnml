/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_diesel_ACTS6700_purchase_black, "gfx/purchase/diesel/ACTS6700_black.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_diesel_ACTS6700_purchase_blue, "gfx/purchase/diesel/ACTS6700_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_diesel_ACTS6700_purchase, current_year) {
	0..2011: spriteset_diesel_ACTS6700_purchase_black;
    spriteset_diesel_ACTS6700_purchase_blue;
}

/*
* VEHICLE
*/

spriteset(spriteset_diesel_ACTS6700_blue, "gfx/diesel/ACTS6700.png") {
	template_2cc_78(1, 1)
}
spriteset(spriteset_diesel_ACTS6700_black, "gfx/diesel/ACTS6700.png") {
	template_2cc_78(1, 32)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_diesel_ACTS6700_randomlivery) {
	4: spriteset_diesel_ACTS6700_blue;
    1: spriteset_diesel_ACTS6700_black;
}

switch(FEAT_TRAINS, SELF, switch_diesel_ACTS6700_autolivery, date_of_last_service) {
	0..date(2011,12,31): switch_diesel_ACTS6700_randomlivery;
    spriteset_diesel_ACTS6700_blue;
}

switch(FEAT_TRAINS, SELF, switch_diesel_ACTS6700_purchaselivery, build_year) {
	0..2011: switch_diesel_ACTS6700_randomlivery;
    spriteset_diesel_ACTS6700_blue;
}

switch(FEAT_TRAINS, SELF, switch_diesel_ACTS6700_livery, cargo_subtype) {
	1: switch_diesel_ACTS6700_purchaselivery;
    2: spriteset_diesel_ACTS6700_blue;
    3: spriteset_diesel_ACTS6700_black;
    switch_diesel_ACTS6700_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_diesel_ACTS6700_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_ACTS);
    3: return string(str_cargo_subtype_VOSlogistics);
    return CB_RESULT_NO_TEXT;
}
