/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_metro_GVB_M1M2_purchase_red, "gfx/purchase/metro/GVB_M1M2_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_metro_GVB_M1M2_purchase_blue, "gfx/purchase/metro/GVB_M1M2_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_metro_GVB_M1M2_purchase, current_year) {
	0..2007: spriteset_metro_GVB_M1M2_purchase_red;
    spriteset_metro_GVB_M1M2_purchase_blue;
}

/*
* VEHICLE
*/

spriteset(spriteset_metro_GVB_M1M2_blue_front, "gfx/metro/GVB_M1M2.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_metro_GVB_M1M2_blue_back, "gfx/metro/GVB_M1M2.png") {
	template_2cc_88(1, 32)
}

spriteset(spriteset_metro_GVB_M1M2_red_front, "gfx/metro/GVB_M1M2.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_metro_GVB_M1M2_red_back, "gfx/metro/GVB_M1M2.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_position_blue, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_GVB_M1M2_blue_back;
    spriteset_metro_GVB_M1M2_blue_front;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_position_red, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_GVB_M1M2_red_back;
    spriteset_metro_GVB_M1M2_red_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_metro_GVB_M1M2_autolivery, date_of_last_service) {
	0..date(2007,12,31): switch_metro_GVB_M1M2_position_red;
    switch_metro_GVB_M1M2_position_blue;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_purchaselivery, build_year) {
	0..2007: switch_metro_GVB_M1M2_position_red;
    switch_metro_GVB_M1M2_position_blue;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_livery, cargo_subtype) {
	1: switch_metro_GVB_M1M2_purchaselivery;
    2: switch_metro_GVB_M1M2_position_red;
    3: switch_metro_GVB_M1M2_position_blue;
    switch_metro_GVB_M1M2_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_silver_red);
    3: return string(str_cargo_subtype_silver_blue);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M1M2_articulated, extra_callback_info1) {
    1: return item_metro_GVB_M1M2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
