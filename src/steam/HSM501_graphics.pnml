/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_steam_HSM501_purchase, "gfx/purchase/steam/HSM501.png") {
	template_purchase(0, 0)
}

spriteset(spriteset_steam_NS2100_purchase, "gfx/purchase/steam/NS2100.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_livery_purchase, current_year) {
	0..1919: spriteset_steam_HSM501_purchase;
    spriteset_steam_NS2100_purchase;
}

/*
* VEHICLE
*/

//6x engine animation
spriteset(spriteset_steam_HSM501_ani_1, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 1)
}
spriteset(spriteset_steam_HSM501_ani_2, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 32)
}
spriteset(spriteset_steam_HSM501_ani_3, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 64)
}
spriteset(spriteset_steam_HSM501_ani_4, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 96)
}
spriteset(spriteset_steam_HSM501_ani_5, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 128)
}
spriteset(spriteset_steam_HSM501_ani_6, "gfx/steam/HSM501.png") {
	template_2cc_58(1, 160)
}
//tender
spriteset(spriteset_steam_HSM501_tender, "gfx/steam/HSM501.png") {
	template_2cc_48(1, 192)
}

switch(FEAT_TRAINS, PARENT, switch_steam_HSM501_animation, (motion_counter)) {
    2:      spriteset_steam_HSM501_ani_2;
    3:      spriteset_steam_HSM501_ani_3;
    4..5:   spriteset_steam_HSM501_ani_4;
    6:      spriteset_steam_HSM501_ani_5;
    7:      spriteset_steam_HSM501_ani_6;
    10:     spriteset_steam_HSM501_ani_2;
    11:     spriteset_steam_HSM501_ani_3;
    12..13: spriteset_steam_HSM501_ani_4;
    14:     spriteset_steam_HSM501_ani_5;
    15:     spriteset_steam_HSM501_ani_6;
    spriteset_steam_HSM501_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_HSM501_tender;
    switch_steam_HSM501_animation;
}

//6x engine animation
spriteset(spriteset_steam_NS2100_ani_1, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 1)
}
spriteset(spriteset_steam_NS2100_ani_2, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 32)
}
spriteset(spriteset_steam_NS2100_ani_3, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 64)
}
spriteset(spriteset_steam_NS2100_ani_4, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 96)
}
spriteset(spriteset_steam_NS2100_ani_5, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 128)
}
spriteset(spriteset_steam_NS2100_ani_6, "gfx/steam/NS2100.png") {
	template_2cc_58(1, 160)
}
//tender
spriteset(spriteset_steam_NS2100_tender, "gfx/steam/NS2100.png") {
	template_2cc_48(1, 192)
}

switch(FEAT_TRAINS, PARENT, switch_steam_NS2100_animation, (motion_counter)) {
    2:      spriteset_steam_NS2100_ani_2;
    3:      spriteset_steam_NS2100_ani_3;
    4..5:   spriteset_steam_NS2100_ani_4;
    6:      spriteset_steam_NS2100_ani_5;
    7:      spriteset_steam_NS2100_ani_6;
    10:     spriteset_steam_NS2100_ani_2;
    11:     spriteset_steam_NS2100_ani_3;
    12..13: spriteset_steam_NS2100_ani_4;
    14:     spriteset_steam_NS2100_ani_5;
    15:     spriteset_steam_NS2100_ani_6;
    spriteset_steam_NS2100_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_NS2100_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_NS2100_tender;
    switch_steam_NS2100_animation;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_steam_HSM501_autolivery, date_of_last_service) {
	0..date(1919,12,31): switch_steam_HSM501_position;
    switch_steam_NS2100_position;
}

switch(FEAT_TRAINS, PARENT, switch_steam_HSM501_purchaselivery, build_year) {
	0..1919: switch_steam_HSM501_position;
    switch_steam_NS2100_position;
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_livery, cargo_subtype) {
	1: switch_steam_HSM501_purchaselivery;
    2: switch_steam_HSM501_position;
    3: switch_steam_NS2100_position;
    switch_steam_HSM501_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_steam_HSM501_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_HSM);
    3: return string(str_cargo_subtype_NS);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_articulated, extra_callback_info1) {
    1: return item_steam_HSM501;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VEHICLE LENGTH CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_length, (position_in_vehid_chain % 2)) {
    1: return 4;
    return 5;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_HSM501_visual_effect_and_powered, (position_in_vehid_chain % 2)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
}
