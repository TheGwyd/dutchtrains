/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_GTW28_purchase_arriva, "gfx/purchase/dmu/GTW28_arriva.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_GTW28_purchase_veolia, "gfx/purchase/dmu/GTW28_veolia.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_GTW28_purchase_breng, "gfx/purchase/dmu/GTW28_breng.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_dmu_GTW28_purchase, current_year) {
	0..2006: spriteset_dmu_GTW28_purchase_arriva;
    2007..2012: spriteset_dmu_GTW28_purchase_veolia;
    spriteset_dmu_GTW28_purchase_breng;
}

/*
* VEHICLE
*/

//spurt
spriteset(spriteset_dmu_GTW_spurt_front, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_dmu_GTW_spurt_back, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_dmu_GTW_spurt_middle, "gfx/dmu/GTW.png") {
	template_2cc_78(1, 64)
}
spriteset(spriteset_dmu_GTW_spurt_powerpack_dmu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 96)
}
spriteset(spriteset_dmu_GTW_spurt_powerpack_emu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 128)
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_position_spurt, (position_in_vehid_chain % 4)) {
	1: spriteset_dmu_GTW_spurt_powerpack_dmu;
    2: spriteset_dmu_GTW_spurt_middle;
    3: spriteset_dmu_GTW_spurt_back;
    spriteset_dmu_GTW_spurt_front;
}

//velios
spriteset(spriteset_dmu_GTW_velios_front, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 160)
}
spriteset(spriteset_dmu_GTW_velios_back, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 192)
}
spriteset(spriteset_dmu_GTW_velios_middle, "gfx/dmu/GTW.png") {
	template_2cc_78(1, 224)
}
spriteset(spriteset_dmu_GTW_velios_powerpack_dmu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 256)
}
/* spriteset(spriteset_dmu_GTW_velios_powerpack_emu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 288)
} */
spriteset(spriteset_dmu_GTW_velios_powerpack2_dmu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 576)
}
spriteset(spriteset_dmu_GTW_velios_powerpack2_emu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 608)
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_position_velios, (position_in_vehid_chain % 4)) {
	1: spriteset_dmu_GTW_velios_powerpack_dmu;
    2: spriteset_dmu_GTW_velios_middle;
    3: spriteset_dmu_GTW_velios_back;
    spriteset_dmu_GTW_velios_front;
}
switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_position_velios2, (position_in_vehid_chain % 4)) {
	1: spriteset_dmu_GTW_velios_powerpack2_dmu;
    2: spriteset_dmu_GTW_velios_middle;
    3: spriteset_dmu_GTW_velios_back;
    spriteset_dmu_GTW_velios_front;
}

//breng
spriteset(spriteset_dmu_GTW_breng_front, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 320)
}
spriteset(spriteset_dmu_GTW_breng_back, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 352)
}
spriteset(spriteset_dmu_GTW_breng_middle, "gfx/dmu/GTW.png") {
	template_2cc_78(1, 384)
}
spriteset(spriteset_dmu_GTW_breng_powerpack_dmu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 416)
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_position_breng, (position_in_vehid_chain % 4)) {
	1: spriteset_dmu_GTW_breng_powerpack_dmu;
    2: spriteset_dmu_GTW_breng_middle;
    3: spriteset_dmu_GTW_breng_back;
    spriteset_dmu_GTW_breng_front;
}

//vechtdallijnen (Arriva)
spriteset(spriteset_dmu_GTW_vechtdallijnen_front, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 448)
}
spriteset(spriteset_dmu_GTW_vechtdallijnen_back, "gfx/dmu/GTW.png") {
	template_2cc_88(1, 480)
}
spriteset(spriteset_dmu_GTW_vechtdallijnen_middle, "gfx/dmu/GTW.png") {
	template_2cc_78(1, 512)
}
spriteset(spriteset_dmu_GTW_vechtdallijnen_powerpack_emu, "gfx/dmu/GTW.png") {
	template_2cc_38(1, 544)
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_GTW28_velios, date_of_last_service) {
	0..date(2007,12,31): switch_dmu_GTW28_position_velios;
    switch_dmu_GTW28_position_velios2;
}

random_switch(FEAT_TRAINS, PARENT, switch_dmu_GTW28_randomlivery_arriva_veolia_breng) {
	1: switch_dmu_GTW28_position_spurt;
    1: switch_dmu_GTW28_velios;
    1: switch_dmu_GTW28_position_breng;
}

random_switch(FEAT_TRAINS, PARENT, switch_dmu_GTW28_randomlivery_arriva_veolia) {
	1: switch_dmu_GTW28_position_spurt;
    1: switch_dmu_GTW28_velios;
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_randomlivery, build_year) {
	0..2006: switch_dmu_GTW28_position_spurt;
    2007..2012: switch_dmu_GTW28_randomlivery_arriva_veolia;
    switch_dmu_GTW28_randomlivery_arriva_veolia_breng;
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_livery, cargo_subtype) {
    1: switch_dmu_GTW28_position_spurt;
    2: switch_dmu_GTW28_velios;
    3: switch_dmu_GTW28_position_breng;
    switch_dmu_GTW28_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
    1: return string(str_cargo_subtype_Arriva_spurt);
    2: return string(str_cargo_subtype_Veolia_velios);
    3: return string(str_cargo_subtype_Breng);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_articulated, extra_callback_info1) {
    1..3: return item_dmu_GTW28;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* SHORTEN VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_length, (position_in_vehid_chain % 4)) {
    1: return 3;
    2: return 7;
    return 8;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW28_visual_effect_and_powered, (position_in_vehid_chain % 4)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
