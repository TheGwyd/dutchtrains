/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_metro_GVB_M4S3_purchase_blue_red, "gfx/purchase/metro/GVB_M4S3_blue_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_metro_GVB_M4S3_purchase_blue, "gfx/purchase/metro/GVB_M4S3_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_metro_GVB_M4S3_purchase, current_year) {
	0..2005: spriteset_metro_GVB_M4S3_purchase_blue_red;
    spriteset_metro_GVB_M4S3_purchase_blue;
}

/*
* VEHICLE
*/

spriteset(spriteset_metro_GVB_M4S3_blue_front, "gfx/metro/GVB_M4S3.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_metro_GVB_M4S3_blue_back, "gfx/metro/GVB_M4S3.png") {
	template_2cc_88(1, 32)
}

spriteset(spriteset_metro_GVB_M4S3_red_front, "gfx/metro/GVB_M4S3.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_metro_GVB_M4S3_red_back, "gfx/metro/GVB_M4S3.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_position_blue, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_GVB_M4S3_blue_back;
    spriteset_metro_GVB_M4S3_blue_front;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_position_red, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_GVB_M4S3_red_back;
    spriteset_metro_GVB_M4S3_red_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_metro_GVB_M4S3_autolivery, date_of_last_service) {
	0..date(2005,12,31): switch_metro_GVB_M4S3_position_red;
    switch_metro_GVB_M4S3_position_blue;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_purchaselivery, build_year) {
	0..2005: switch_metro_GVB_M4S3_position_red;
    switch_metro_GVB_M4S3_position_blue;
}

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_livery, cargo_subtype) {
	1: switch_metro_GVB_M4S3_purchaselivery;
    2: switch_metro_GVB_M4S3_position_red;
    3: switch_metro_GVB_M4S3_position_blue;
    switch_metro_GVB_M4S3_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_red_blue);
    3: return string(str_cargo_subtype_blue);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_metro_GVB_M4S3_articulated, extra_callback_info1) {
    1: return item_metro_GVB_M4S3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
