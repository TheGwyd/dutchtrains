/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_Mat35ELD2_purchase_lightroof, "gfx/purchase/emu/Mat35ELD2_lightroof.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_emu_Mat35ELD2_purchase_darkroof, "gfx/purchase/emu/Mat35ELD2_darkroof.png") {
	template_purchase(0, 0)
}
switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2_purchase, current_year) {
	0..1938: spriteset_emu_Mat35ELD2_purchase_lightroof;
    spriteset_emu_Mat35ELD2_purchase_darkroof;
}

/*
* VEHICLE
*/

//silver roof
spriteset(spriteset_emu_Mat35EL2_silverroof_front, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_emu_Mat35ELD2_silverroof_front, "gfx/emu/Mat35ELD2.png") {
	template_2cc_88(1, 1)
}

spriteset(spriteset_emu_Mat35EL2_silverroof_back, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 32)
}

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2_silverroof_front, cargo_classes_in_consist) {
    bitmask(CC_PASSENGERS,CC_MAIL): spriteset_emu_Mat35ELD2_silverroof_front;
    bitmask(CC_PASSENGERS,CC_EXPRESS): spriteset_emu_Mat35ELD2_silverroof_front;
    spriteset_emu_Mat35EL2_silverroof_front;
}

//grey roof
spriteset(spriteset_emu_Mat35ELD2_greyroof_front, "gfx/emu/Mat35ELD2.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_emu_Mat35EL2_greyroof_front, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 64)
}

spriteset(spriteset_emu_Mat35EL2_greyroof_back, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2_greyroof_front, cargo_classes_in_consist) {
    bitmask(CC_PASSENGERS,CC_MAIL): spriteset_emu_Mat35ELD2_greyroof_front;
    bitmask(CC_PASSENGERS,CC_EXPRESS): spriteset_emu_Mat35ELD2_greyroof_front;
    spriteset_emu_Mat35EL2_greyroof_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2_autolivery, date_of_last_service) {
	0..date(1938,12,31): switch_emu_Mat35ELD2_silverroof_front;
    switch_emu_Mat35ELD2_greyroof_front;
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2_purchaselivery, build_year) {
	0..1938: switch_emu_Mat35ELD2_silverroof_front;
    switch_emu_Mat35ELD2_greyroof_front;
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2_livery, cargo_subtype) {
	1: switch_emu_Mat35ELD2_purchaselivery;
    2: switch_emu_Mat35ELD2_silverroof_front;
    3: switch_emu_Mat35ELD2_greyroof_front;
    switch_emu_Mat35ELD2_autolivery;
}

// LIVERY SELECTION D-part

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2D_autolivery, date_of_last_service) {
	0..date(1938,12,31): spriteset_emu_Mat35EL2_silverroof_back;
    spriteset_emu_Mat35EL2_greyroof_back;
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2D_purchaselivery, build_year) {
	0..1938: spriteset_emu_Mat35EL2_silverroof_back;
    spriteset_emu_Mat35EL2_greyroof_back;
}

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35ELD2D_livery, cargo_subtype) {
	1: switch_emu_Mat35ELD2D_purchaselivery;
    2: spriteset_emu_Mat35EL2_silverroof_back;
    3: spriteset_emu_Mat35EL2_greyroof_back;
    switch_emu_Mat35ELD2D_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_olive_silverroof);
    3: return string(str_cargo_subtype_olive_greyroof);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2_articulated, extra_callback_info1) {
    1: return item_emu_Mat35ELD2D;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* D-part CAPACITY CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_Mat35ELD2D_capacity, cargo_classes) {
    bitmask(CC_PASSENGERS): return 24;
    bitmask(CC_MAIL): return 24;
    return 12;
}
