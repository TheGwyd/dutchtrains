/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_Mat36EL2_purchase_olive_lightroof, "gfx/purchase/emu/Mat36EL2_olive_lightroof.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_emu_Mat36EL2_purchase_olive_darkroof, "gfx/purchase/emu/Mat36EL2_olive_darkroof.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_emu_Mat36EL2_purchase_green_darkroof, "gfx/purchase/emu/Mat36EL2_green_darkroof.png") {
	template_purchase(0, 0)
}
switch(FEAT_TRAINS, PARENT, switch_emu_Mat36EL2_purchase, build_year) {
	0..1938: spriteset_emu_Mat36EL2_purchase_olive_lightroof;
	1939..1955: spriteset_emu_Mat36EL2_purchase_olive_darkroof;
    spriteset_emu_Mat36EL2_purchase_green_darkroof;
}

/*
* VEHICLE
*/

//silver roof
spriteset(spriteset_emu_Mat36EL2_silverroof_front, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_emu_Mat36EL2_silverroof_back, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 32)
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_silverroof, (position_in_vehid_chain % 2)) {
	1: spriteset_emu_Mat36EL2_silverroof_back;
    spriteset_emu_Mat36EL2_silverroof_front;
}

//grey roof
spriteset(spriteset_emu_Mat36EL2_greyroof_front, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_emu_Mat36EL2_greyroof_back, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_greyroof, (position_in_vehid_chain % 2)) {
	1: spriteset_emu_Mat36EL2_greyroof_back;
    spriteset_emu_Mat36EL2_greyroof_front;
}

//grass green
spriteset(spriteset_emu_Mat36EL2_green_front, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_emu_Mat36EL2_green_back, "gfx/emu/Mat36EL2.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_green, (position_in_vehid_chain % 2)) {
	1: spriteset_emu_Mat36EL2_green_back;
    spriteset_emu_Mat36EL2_green_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_emu_Mat36EL2_autolivery, date_of_last_service) {
	0..date(1938,12,31): switch_emu_Mat36EL2_silverroof;
	date(1939,1,1)..date(1955,12,31): switch_emu_Mat36EL2_greyroof;
    switch_emu_Mat36EL2_green;
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_purchaselivery, build_year) {
	0..1938: switch_emu_Mat36EL2_silverroof;
	1939..1955: switch_emu_Mat36EL2_greyroof;
    switch_emu_Mat36EL2_green;
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_livery, cargo_subtype) {
	1: switch_emu_Mat36EL2_purchaselivery;
    2: switch_emu_Mat36EL2_silverroof;
    3: switch_emu_Mat36EL2_greyroof;
    4: switch_emu_Mat36EL2_green;
    switch_emu_Mat36EL2_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_olive_silverroof);
    3: return string(str_cargo_subtype_olive_greyroof);
    4: return string(str_cargo_subtype_green_darkroof);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_Mat36EL2_articulated, extra_callback_info1) {
    1: return item_emu_Mat36EL2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
