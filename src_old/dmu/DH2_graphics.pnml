/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_DH2_orig_purchase, "gfx/purchase/dmu/DH2_orig.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DH2_renew_purchase, "gfx/purchase/dmu/DH2_renew.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_livery_purchase, current_year) {
	0..1996: spriteset_dmu_DH2_orig_purchase;
    spriteset_dmu_DH2_renew_purchase;
}

/*
* VEHICLE
*/

//gen1
spriteset(spriteset_dmu_DH2_orig_front, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_dmu_DH2_orig_back, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 32)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_position_gen1, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DH2_orig_back;
    spriteset_dmu_DH2_orig_front;
}

//gen2
spriteset(spriteset_dmu_DH2_renew_front, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_dmu_DH2_renew_back, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_position_gen2, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DH2_renew_back;
    spriteset_dmu_DH2_renew_front;
}

//wadkikker
spriteset(spriteset_dmu_DH2_wadkikker_front, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 192)
}
spriteset(spriteset_dmu_DH2_wadkikker_back, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 224)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_position_wadkikker, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DH2_wadkikker_back;
    spriteset_dmu_DH2_wadkikker_front;
}

//maashopper
spriteset(spriteset_dmu_DH2_maashopper_front, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_dmu_DH2_maashopper_back, "gfx/dmu/DH2.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_position_maashopper, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DH2_maashopper_back;
    spriteset_dmu_DH2_maashopper_front;
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_dmu_DH2_randomlivery_wadkikker_maashopper) {
	29: switch_dmu_DH2_position_gen2;
    1: switch_dmu_DH2_position_wadkikker;
    1: switch_dmu_DH2_position_maashopper;
}

random_switch(FEAT_TRAINS, PARENT, switch_dmu_DH2_randomlivery_wadkikker) {
	30: switch_dmu_DH2_position_gen2;
    1: switch_dmu_DH2_position_wadkikker;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DH2_autolivery, date_of_last_service) {
	0..date(1996,12,31): switch_dmu_DH2_position_gen1;
    date(1997,12,31)..date(1998,12,31): switch_dmu_DH2_position_gen2;
    date(1999,12,31)..date(2006,12,31): switch_dmu_DH2_randomlivery_wadkikker;
    switch_dmu_DH2_randomlivery_wadkikker_maashopper;
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_purchaselivery, build_year) {
	0..1996: switch_dmu_DH2_position_gen1;
    1997..1998: switch_dmu_DH2_position_gen2;
    1999..2006: switch_dmu_DH2_randomlivery_wadkikker;
    switch_dmu_DH2_randomlivery_wadkikker_maashopper;
}

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_livery, cargo_subtype) {
	1: switch_dmu_DH2_purchaselivery;
    2: switch_dmu_DH2_position_gen1;
    3: switch_dmu_DH2_position_gen2;
    4: switch_dmu_DH2_position_wadkikker;
    5: switch_dmu_DH2_position_maashopper;
    switch_dmu_DH2_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_DH2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_original);
    3: return string(str_cargo_subtype_renewal);
    4: return string(str_cargo_subtype_wadkikker);
    5: return string(str_cargo_subtype_maashopper);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DH2_articulated, extra_callback_info1) {
    1: return item_dmu_DH2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/
switch(FEAT_TRAINS, SELF, switch_dmu_DH2_visual_effect_and_powered, (position_in_vehid_chain % 2)) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
}
