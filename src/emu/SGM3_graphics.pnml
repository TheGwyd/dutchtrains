/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_SGM3_purchase_original, "gfx/purchase/emu/SGM3_original.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_emu_SGM3_purchase_renewal, "gfx/purchase/emu/SGM3_renewal.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_emu_SGM3_purchase, current_year) {
	0..2003: spriteset_emu_SGM3_purchase_original;
    spriteset_emu_SGM3_purchase_renewal;
}

/*
* VEHICLE
*/

//original sgm
spriteset(spriteset_emu_SGM3_sgm_front, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_emu_SGM3_sgm_middle, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_emu_SGM3_sgm_back, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 64)
}

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_sgm, (position_in_vehid_chain % 3)) {
	1: spriteset_emu_SGM3_sgm_middle;
	2: spriteset_emu_SGM3_sgm_back;
    spriteset_emu_SGM3_sgm_front;
}

// sgmm
spriteset(spriteset_emu_SGM3_sgmm_front, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 96)
}
spriteset(spriteset_emu_SGM3_sgmm_middle, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_emu_SGM3_sgmm_back, "gfx/emu/SGM3.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_sgmm, (position_in_vehid_chain % 3)) {
	1: spriteset_emu_SGM3_sgmm_middle;
	2: spriteset_emu_SGM3_sgmm_back;
    spriteset_emu_SGM3_sgmm_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_emu_SGM3_autolivery, date_of_last_service) {
	0..date(2003,12,31): switch_emu_SGM3_sgm;
    switch_emu_SGM3_sgmm;
}

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_purchaselivery, build_year) {
	0..2003: switch_emu_SGM3_sgm;
    switch_emu_SGM3_sgmm;
}

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_livery, cargo_subtype) {
	1: switch_emu_SGM3_purchaselivery;
    2: switch_emu_SGM3_sgm;
    3: switch_emu_SGM3_sgmm;
    switch_emu_SGM3_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_SGM3_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_yellow_blue);
    3: return string(str_cargo_subtype_white_yellow);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_articulated, extra_callback_info1) {
    1..2: return item_emu_SGM3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* CARGO CAPACITY CALLBACK
*/

switch(FEAT_TRAINS, PARENT, switch_emu_SGM3_purchasecapacity, build_year) {
	0..2003: return 83;
    return 74;
}

switch(FEAT_TRAINS, PARENT, switch_emu_SGM3_capacity, cargo_subtype) {
	1: switch_emu_SGM3_purchasecapacity;
    2: return 83;
    3: return 74;
    return 83; //used for autolivery, changing capacity without refitting is not allowed
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_SGM3_visual_effect_and_powered, (position_in_vehid_chain % 3)) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
