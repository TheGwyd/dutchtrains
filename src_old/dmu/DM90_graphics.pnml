/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_DM90_purchase, "gfx/purchase/dmu/DM90.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DM90_purchase_whiteroof, "gfx/purchase/dmu/DM90_whiteroof.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_livery_purchase, current_year) {
	0..2001: spriteset_dmu_DM90_purchase;
    spriteset_dmu_DM90_purchase_whiteroof;
}

/*
* VEHICLE
*/

//ns
spriteset(spriteset_dmu_DM90_ns_front, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_dmu_DM90_ns_back, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 32)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_position_ns, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DM90_ns_back;
    spriteset_dmu_DM90_ns_front;
}

//ns with white roof
spriteset(spriteset_dmu_DM90_ns_whiteroof_front, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_dmu_DM90_ns_whiteroof_back, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_position_ns_whiteroof, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DM90_ns_whiteroof_back;
    spriteset_dmu_DM90_ns_whiteroof_front;
}

//syntus
spriteset(spriteset_dmu_DM90_syntus_front, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_dmu_DM90_syntus_back, "gfx/dmu/DM90.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_position_syntus, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DM90_syntus_back;
    spriteset_dmu_DM90_syntus_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_DM90_autolivery, date_of_last_service) {
	0..date(2001,12,31): switch_dmu_DM90_position_ns;
    switch_dmu_DM90_position_ns_whiteroof;
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_purchaselivery, build_year) {
	0..2001: switch_dmu_DM90_position_ns;
    switch_dmu_DM90_position_ns_whiteroof;
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_livery, cargo_subtype) {
	1: switch_dmu_DM90_purchaselivery;
    2: switch_dmu_DM90_position_ns;
    3: switch_dmu_DM90_position_ns_whiteroof;
    4: switch_dmu_DM90_position_syntus;
    switch_dmu_DM90_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_DM90_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_NS);
    3: return string(str_cargo_subtype_NS_whiteroof);
    4: return string(str_cargo_subtype_Syntus);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_articulated, extra_callback_info1) {
    1: return item_dmu_DM90;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
