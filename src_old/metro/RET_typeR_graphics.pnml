/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_metro_RET_typeR_silvergreen_purchase, "gfx/purchase/metro/RET_TypeR_silvergreen.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_metro_RET_typeR_randstadrail_purchase, "gfx/purchase/metro/RET_TypeR_randstadrail.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeR_purchase, current_year) {
	0..2008: spriteset_metro_RET_typeR_randstadrail_purchase;
    spriteset_metro_RET_typeR_silvergreen_purchase;
}

/*
* VEHICLE
*/

spriteset(spriteset_metro_RET_typeR_front_blue, "gfx/metro/RET_typeR.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_metro_RET_typeR_middle_blue, "gfx/metro/RET_typeR.png") {
	template_2cc_48(1, 32)
}
spriteset(spriteset_metro_RET_typeR_back_blue, "gfx/metro/RET_typeR.png") {
	template_2cc_68(1, 64)
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_position_blue, (position_in_vehid_chain % 3)) {
	1: spriteset_metro_RET_typeR_middle_blue;
    2: spriteset_metro_RET_typeR_back_blue;
    spriteset_metro_RET_typeR_front_blue;
}

spriteset(spriteset_metro_RET_typeR_front_silver, "gfx/metro/RET_typeR.png") {
	template_2cc_68(1, 96)
}
spriteset(spriteset_metro_RET_typeR_middle_silver, "gfx/metro/RET_typeR.png") {
	template_2cc_48(1, 128)
}
spriteset(spriteset_metro_RET_typeR_back_silver, "gfx/metro/RET_typeR.png") {
	template_2cc_68(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_position_silver, (position_in_vehid_chain % 3)) {
	1: spriteset_metro_RET_typeR_middle_silver;
    2: spriteset_metro_RET_typeR_back_silver;
    spriteset_metro_RET_typeR_front_silver;
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeR_random_ret_randstadrail) {
	1: switch_metro_RET_typeR_position_blue;
    1: switch_metro_RET_typeR_position_silver;
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_randomlivery, build_year) {
	0..2008: switch_metro_RET_typeR_position_blue;
    switch_metro_RET_typeR_random_ret_randstadrail;
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_livery, cargo_subtype) {
    1: switch_metro_RET_typeR_position_blue;
    2: switch_metro_RET_typeR_position_silver;
    switch_metro_RET_typeR_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
    1: return string(str_cargo_subtype_randstadrail);
    2: return string(str_cargo_subtype_silver_green);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_articulated, extra_callback_info1) {
    1..2: return item_metro_RET_typeR;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* SHORTEN VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeR_length, (position_in_vehid_chain % 3)) {
    1: return 4;
    return 6;
}
