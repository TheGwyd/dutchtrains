/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_coaches_PlanL_purchase_blue, "gfx/purchase/coaches/PlanL_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_coaches_PlanL_purchase_blue_yellow, "gfx/purchase/coaches/PlanL_blue_yellow.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_coaches_PlanL_purchase_yellow_blue, "gfx/purchase/coaches/PlanL_yellow_blue.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_coaches_PlanL_purchase, current_year) {
	0..1973: spriteset_coaches_PlanL_purchase_blue;
	1974..1982: spriteset_coaches_PlanL_purchase_blue_yellow;
    spriteset_coaches_PlanL_purchase_yellow_blue;
}

/*
* VEHICLE
*/

spriteset(spriteset_coaches_PlanL_blue, "gfx/coaches/PlanL.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_coaches_PlanL_blue_yellow, "gfx/coaches/PlanL.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_coaches_PlanL_yellow_blue, "gfx/coaches/PlanL.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_coaches_PlanL_yellow_blue2, "gfx/coaches/PlanL.png") {
	template_2cc_88(1, 96)
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, SELF, switch_coaches_PlanL_random_yellow_blue) {
	1: spriteset_coaches_PlanL_yellow_blue;
	1: spriteset_coaches_PlanL_yellow_blue2;
}

switch(FEAT_TRAINS, PARENT, switch_coaches_PlanL_autolivery, date_of_last_service) {
	0..date(1979,12,31): spriteset_coaches_PlanL_blue;
	date(1980,12,31)..date(1982,12,31): spriteset_coaches_PlanL_blue_yellow;
    switch_coaches_PlanL_random_yellow_blue;
}

switch(FEAT_TRAINS, SELF, switch_coaches_PlanL_purchaselivery, build_year) {
	0..1979: spriteset_coaches_PlanL_blue;
	1980..1982: spriteset_coaches_PlanL_blue_yellow;
    switch_coaches_PlanL_random_yellow_blue;
}

switch(FEAT_TRAINS, SELF, switch_coaches_PlanL_livery, cargo_subtype) {
	1: switch_coaches_PlanL_purchaselivery;
    2: spriteset_coaches_PlanL_blue;
    3: spriteset_coaches_PlanL_blue_yellow;
    4: switch_coaches_PlanL_random_yellow_blue;
    switch_coaches_PlanL_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_coaches_PlanL_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
    1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_blue);
    3: return string(str_cargo_subtype_benelux);
    4: return string(str_cargo_subtype_yellow_blue_ic);
    return CB_RESULT_NO_TEXT;
}
