/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_GTW26_purchase_arriva, "gfx/purchase/dmu/GTW26_arriva.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_GTW26_purchase_veolia, "gfx/purchase/dmu/GTW26_veolia.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_GTW26_purchase_veolia2, "gfx/purchase/dmu/GTW26_veolia2.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, PARENT, switch_dmu_GTW26_purchase, current_year) {
	0..2006: spriteset_dmu_GTW26_purchase_arriva;
	2007..2007: spriteset_dmu_GTW26_purchase_veolia;
    spriteset_dmu_GTW26_purchase_veolia2;
}

/*
* VEHICLE
*/

//spurt
//graphics from GTW 2/8

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_position_spurt, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_GTW_spurt_powerpack_dmu;
    2: spriteset_dmu_GTW_spurt_back;
    spriteset_dmu_GTW_spurt_front;
}

//velios
//graphics from GTW 2/8

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_position_velios, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_GTW_velios_powerpack_dmu;
    2: spriteset_dmu_GTW_velios_back;
    spriteset_dmu_GTW_velios_front;
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_position_velios2, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_GTW_velios_powerpack2_dmu;
    2: spriteset_dmu_GTW_velios_back;
    spriteset_dmu_GTW_velios_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_GTW26_velios, date_of_last_service) {
	0..date(2007,12,31): switch_dmu_GTW26_position_velios;
    switch_dmu_GTW26_position_velios2;
}

random_switch(FEAT_TRAINS, PARENT, switch_dmu_GTW26_randomlivery_arriva_veolia) {
	1: switch_dmu_GTW26_position_spurt;
    1: switch_dmu_GTW26_velios;
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_randomlivery, build_year) {
	0..2006: switch_dmu_GTW26_position_spurt;
    switch_dmu_GTW26_randomlivery_arriva_veolia;
}

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_livery, cargo_subtype) {
    1: switch_dmu_GTW26_position_spurt;
    2: switch_dmu_GTW26_velios;
    switch_dmu_GTW26_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
    1: return string(str_cargo_subtype_Arriva_spurt);
    2: return string(str_cargo_subtype_Veolia_velios);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_articulated, extra_callback_info1) {
    1..2: return item_dmu_GTW26;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* SHORTEN VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_length, (position_in_vehid_chain % 3)) {
    1: return 3;
    return 8;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_GTW26_visual_effect_and_powered, (position_in_vehid_chain % 3)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
