/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_steam_HSM126_purchase, "gfx/purchase/steam/HSM126.png") {
	template_purchase(0, 0)
}

spriteset(spriteset_steam_NS1100_purchase, "gfx/purchase/steam/NS1100.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM126_livery_purchase, current_year) {
	0..1919: spriteset_steam_HSM126_purchase;
    spriteset_steam_NS1100_purchase;
}

/*
* VEHICLE
*/

//6x engine animation
spriteset(spriteset_steam_HSM126_ani_1, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 1)
}
spriteset(spriteset_steam_HSM126_ani_2, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 32)
}
spriteset(spriteset_steam_HSM126_ani_3, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 64)
}
spriteset(spriteset_steam_HSM126_ani_4, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 96)
}
spriteset(spriteset_steam_HSM126_ani_5, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 128)
}
spriteset(spriteset_steam_HSM126_ani_6, "gfx/steam/HSM126.png") {
	template_2cc_48(1, 160)
}
//tender
spriteset(spriteset_steam_HSM126_tender, "gfx/steam/HSM126.png") {
	template_2cc_38(1, 192)
}

switch(FEAT_TRAINS, PARENT, switch_steam_HSM126_animation, (motion_counter)) {
    2:      spriteset_steam_HSM126_ani_2;
    3:      spriteset_steam_HSM126_ani_3;
    4..5:   spriteset_steam_HSM126_ani_4;
    6:      spriteset_steam_HSM126_ani_5;
    7:      spriteset_steam_HSM126_ani_6;
    10:     spriteset_steam_HSM126_ani_2;
    11:     spriteset_steam_HSM126_ani_3;
    12..13: spriteset_steam_HSM126_ani_4;
    14:     spriteset_steam_HSM126_ani_5;
    15:     spriteset_steam_HSM126_ani_6;
    spriteset_steam_HSM126_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM126_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_HSM126_tender;
    switch_steam_HSM126_animation;
}

//6x engine animation
spriteset(spriteset_steam_NS1100_ani_1, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 1)
}
spriteset(spriteset_steam_NS1100_ani_2, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 32)
}
spriteset(spriteset_steam_NS1100_ani_3, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 64)
}
spriteset(spriteset_steam_NS1100_ani_4, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 96)
}
spriteset(spriteset_steam_NS1100_ani_5, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 128)
}
spriteset(spriteset_steam_NS1100_ani_6, "gfx/steam/NS1100.png") {
	template_2cc_48(1, 160)
}
//tender
spriteset(spriteset_steam_NS1100_tender, "gfx/steam/NS1100.png") {
	template_2cc_38(1, 192)
}

switch(FEAT_TRAINS, PARENT, switch_steam_NS1100_animation, (motion_counter)) {
    2:      spriteset_steam_NS1100_ani_2;
    3:      spriteset_steam_NS1100_ani_3;
    4..5:   spriteset_steam_NS1100_ani_4;
    6:      spriteset_steam_NS1100_ani_5;
    7:      spriteset_steam_NS1100_ani_6;
    10:     spriteset_steam_NS1100_ani_2;
    11:     spriteset_steam_NS1100_ani_3;
    12..13: spriteset_steam_NS1100_ani_4;
    14:     spriteset_steam_NS1100_ani_5;
    15:     spriteset_steam_NS1100_ani_6;
    spriteset_steam_NS1100_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_NS1100_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_NS1100_tender;
    switch_steam_NS1100_animation;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_steam_HSM126_autolivery, date_of_last_service) {
	0..date(1919,12,31): switch_steam_HSM126_position;
    switch_steam_NS1100_position;
}

switch(FEAT_TRAINS, PARENT, switch_steam_HSM126_purchaselivery, build_year) {
	0..1919: switch_steam_HSM126_position;
    switch_steam_NS1100_position;
}

switch(FEAT_TRAINS, SELF, switch_steam_HSM126_livery, cargo_subtype) {
	1: switch_steam_HSM126_purchaselivery;
    2: switch_steam_HSM126_position;
    3: switch_steam_NS1100_position;
    switch_steam_HSM126_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_steam_HSM126_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_HSM);
    3: return string(str_cargo_subtype_NS);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_HSM126_articulated, extra_callback_info1) {
    1: return item_steam_HSM126;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VEHICLE LENGTH CALLBACK
*/


switch(FEAT_TRAINS, SELF, switch_steam_HSM126_length, (position_in_vehid_chain % 2)) {
    1: return 3;
    return 4;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_HSM126_visual_effect_and_powered, (position_in_vehid_chain % 2)) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
}
