/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_DE3_red_purchase, "gfx/purchase/dmu/DE3_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DE3_yellow_purchase, "gfx/purchase/dmu/DE3_yellow.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_livery_purchase, current_year) {
	0..1968: spriteset_dmu_DE3_red_purchase;
    spriteset_dmu_DE3_yellow_purchase;
}

/*
* VEHICLE
*/

//red
spriteset(spriteset_dmu_DE3_red_front, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_dmu_DE3_red_middle, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 32)
}
spriteset(spriteset_dmu_DE3_red_back, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 64)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_position_red, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_DE3_red_middle;
    2: spriteset_dmu_DE3_red_back;
    spriteset_dmu_DE3_red_front;
}

///red grey doors
spriteset(spriteset_dmu_DE3_redgreydoors_front, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 96)
}
spriteset(spriteset_dmu_DE3_redgreydoors_middle, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_dmu_DE3_redgreydoors_back, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_position_redgreydoors, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_DE3_redgreydoors_middle;
    2: spriteset_dmu_DE3_redgreydoors_back;
    spriteset_dmu_DE3_redgreydoors_front;
}

//yellow
spriteset(spriteset_dmu_DE3_yellow_front, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 192)
}
spriteset(spriteset_dmu_DE3_yellow_middle, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 224)
}
spriteset(spriteset_dmu_DE3_yellow_back, "gfx/dmu/DE3.png") {
	template_2cc_88(1, 256)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_position_yellow, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_DE3_yellow_middle;
    2: spriteset_dmu_DE3_yellow_back;
    spriteset_dmu_DE3_yellow_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_autolivery_red, date_of_last_service) {
	0..date(1971,12,31): switch_dmu_DE3_position_red;
    switch_dmu_DE3_position_redgreydoors;
}
random_switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_autolivery_1969) {
	1: switch_dmu_DE3_autolivery_red;
    1: switch_dmu_DE3_position_yellow;
}
switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_autolivery, date_of_last_service) {
	0..date(1968,12,31): switch_dmu_DE3_position_red;
	date(1969,12,31)..date(1973,12,31): switch_dmu_DE3_autolivery_1969;
    switch_dmu_DE3_position_yellow;
}

random_switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_purchaselivery_1972) {
	1: switch_dmu_DE3_position_red;
    1: switch_dmu_DE3_position_yellow;
}
random_switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_purchaselivery_1969) {
	1: switch_dmu_DE3_position_redgreydoors;
    1: switch_dmu_DE3_position_yellow;
}
switch(FEAT_TRAINS, SELF, switch_dmu_DE3_purchaselivery, build_year) {
	0..1968: switch_dmu_DE3_position_red;
	1969..1971: switch_dmu_DE3_purchaselivery_1969;
	1972..1973: switch_dmu_DE3_purchaselivery_1972;
    switch_dmu_DE3_position_yellow;
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_livery, cargo_subtype) {
	1: switch_dmu_DE3_purchaselivery;
    2: switch_dmu_DE3_position_red;
    3: switch_dmu_DE3_position_redgreydoors;
    4: switch_dmu_DE3_position_yellow;
    switch_dmu_DE3_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_dmu_DE3_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_red);
    3: return string(str_cargo_subtype_red_greydoors);
    4: return string(str_cargo_subtype_yellow);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_articulated, extra_callback_info1) {
    1..2: return item_dmu_DE3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_visual_effect_and_powered, (position_in_vehid_chain % 3)) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
