/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_TGV_purchase, "gfx/purchase/emu/TGV.png") {
	template_purchase(0, 0)
}

/*
* VEHICLE
*/

spriteset(spriteset_emu_TGV_PBKA_front, "gfx/emu/TGV.png") {
	template_2cc_88(0, 0)
}
spriteset(spriteset_emu_TGV_PBKA_back, "gfx/emu/TGV.png") {
	template_2cc_88(0, 30)
}
spriteset(spriteset_emu_TGV_firstcoach, "gfx/emu/TGV.png") {
	template_2cc_88(0, 60)
}
spriteset(spriteset_emu_TGV_lastcoach, "gfx/emu/TGV.png") {
	template_2cc_88(0, 90)
}
spriteset(spriteset_emu_TGV_middlecoach, "gfx/emu/TGV.png") {
	template_2cc_88(0, 120)
}
spriteset(spriteset_emu_TGV_PBA_front, "gfx/emu/TGV.png") {
	template_2cc_88(0, 180)
}
spriteset(spriteset_emu_TGV_PBA_back, "gfx/emu/TGV.png") {
	template_2cc_88(0, 210)
}

//PBKA
switch(FEAT_TRAINS, SELF, switch_emu_TGV_PBKA_position, (position_in_vehid_chain % 10)) {
	1: spriteset_emu_TGV_firstcoach;
    2..7: spriteset_emu_TGV_middlecoach;
    8: spriteset_emu_TGV_lastcoach; 
    9: spriteset_emu_TGV_PBKA_back;
    spriteset_emu_TGV_PBKA_front;
}

//PBA
switch(FEAT_TRAINS, SELF, switch_emu_TGV_PBA_position, (position_in_vehid_chain % 10)) {
	1: spriteset_emu_TGV_firstcoach;
    2..7: spriteset_emu_TGV_middlecoach;
    8: spriteset_emu_TGV_lastcoach; 
    9: spriteset_emu_TGV_PBA_back;
    spriteset_emu_TGV_PBA_front;
}


/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_emu_TGV_randomlivery) {
	17: switch_emu_TGV_PBKA_position;
    10: switch_emu_TGV_PBA_position;
}

switch(FEAT_TRAINS, SELF, switch_emu_TGV_livery, cargo_subtype) {
	1: switch_emu_TGV_PBKA_position;
    2: switch_emu_TGV_PBA_position;
    switch_emu_TGV_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_TGV_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
	1: return string(str_cargo_subtype_TGV_PBKA);
    2: return string(str_cargo_subtype_TGV_PBA);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_TGV_articulated, extra_callback_info1) {
    1..9: return item_emu_TGV;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_TGV_visual_effect_and_powered, (position_in_vehid_chain % 10)) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/*
* POWER CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_TGV_power, current_railtype) {
    SAAA: return 11801;
    SABA: return 11801;
    SACA: return 11801;
    SADA: return 11801;
    SAEA: return 11801;
    SBAA: return 11801;
    SBBA: return 11801;
    SBCA: return 11801;
    SBDA: return 11801;
    SBEA: return 11801;
    SAAa: return 6921;
    SABa: return 6921;
    SACa: return 6921;
    SADa: return 6921;
    SAEa: return 6921;
    SBAa: return 6921;
    SBBa: return 6921;
    SBCa: return 6921;
    SBDa: return 6921;
    SBEa: return 6921;
    return 4935;
}
